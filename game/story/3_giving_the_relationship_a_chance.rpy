label giving_the_relationship_a_chance:
    play music "assets/audio/music/bgm.mp3" volume 1.0 fadeout 0.2
    """
    Icarus, or at least that's how you can call him, didn't explain a lot but you've already let your intuition decide what to do about him before your mind really does. 

    If all he feels compelled to do are paper cranes and no single thing or motivation about his previous life,  he's rendered harmless. no reason to go against you.

    Seems like he's already getting used to being in this room, which renders you uneasy.

    You'd rather have him stay elsewhere, somewhere he doesn't interrupt as you deal with boxes, furniture, or electronics.
    """
    scene door with dissolve
    with fade
    
    ILORA "Icarus?"
    show i serious idle at center with dissolve
    "He eyes you with curiosity, slightly uncertain."
    show i happy
    ICARUS "Yes?"

    ILORA "Do you mind moving to the other room?"
    show i -happy thinking_ worry
    "He looks down."

    ICARUS "Umm, yes, sure."

    ILORA "It's just I don't want to accidentally hit you with a racket as I'm moving around or something."
    show i -worry content
    "He looks up at you again, eyes lit up."
    show i -thinking_ content_ giggle
    ICARUS "Don't worry about that! I can stay fully eh… abstract."
    show i -giggle 
    ICARUS "And– And I can even help you if not. My grain of salt for sharing the apartment?"

    ILORA "Sharing?"

    "He's planning on staying? Well, thinking again, he was probably here before you were."
    show i thinking_ -content_
    ICARUS "I'm sorry I can't help you with the rent. I heard the landowner talk about prices the other day. It sounds complicated."

    "You shake your head."

    ILORA "I've been to many places and this is the least problematic one I've stayed in so far. Besides, I already paid for this place."
    show i -thinking_ light idle
    "You're not sure what having \"paid\" for this condo means to him, but his face illuminates. "
    show i excited
    ICARUS "Does that mean we're… roommates?"

    ILORA "Hey, I don't really know you so…"
    show i -excited sad_
    "The sight of him is pitiful as the color seems to drain out of him the longer you go on."
    show i excited 
    ILORA "I'll think about it."
    show i content_
    "He smiles."
    show i closer -excited 
    ICARUS "[ILORA.name], would you like me to help you unpack your things?"
    show i -content_ happy idle 
    ILORA "Hmm…"

    "He is serious about trying to be helpful."

    "But your things are your things, and you are not willing to sacrifice a single bit of your privacy."
    show i serious
    ILORA "Thanks, but I'm alright."
    show i happy -serious
    "You don't miss how his smile dulls despite remaining intact, yet you motion towards the doorway. He silently follows–"
    scene door with blink

    show i -happy surprise idle at center:
        yalign 0.3 zoom 1.5
    "-BANG!"
    show i hurt
    ICARUS "??? Ouch…"
    show i -surprise hurt_ close
    "Confused, you watch him rub his forehead."

    ILORA "???"
    show i -close -hurt shy_
    "He smiles sheepishly."
    show i -shy_ thinking_
    ICARUS "It seems like I can't go through it."

    ILORA "You… You did open the door, right?"
    show i hurt 
    ICARUS "Of course! Even if I didn't, I'm a ghost."

    "And that is why you cannot not think he's messing with you."

    ILORA "What about the window?"

    "He gently presses his hand against an invisible wall where the way out the window is supposed to be."

    ILORA "Ugh…"

    ILORA "Balcony?"
    show i -hurt -thinking startle light serious idle
    
    scene balcony_art with dissolve
    "Obediently, he slides towards the balcony, an arm outstretched before he hits his face. However, he never hits a surface. He looks surprised as he turns around and looks at you from the balcony."
    show i happy
    ICARUS "It's been a while since I had fresh air…"

    ILORA "Perfect then."

    "You follow him out, and nod to yourself as you look at the grand sky."

    ILORA "You can fly off from here."
    show i -smile serious startle idle
    ICARUS "Huh?"

    ILORA "Or can drop down a few floors if flying and potentially bumping with a bird isn't your thing."
    show i -startle surprise_
    ICARUS "WHAT?"

    ILORA "What?"
    show i -surprise_ worry_ 
    ICARUS "I– I…"

    "He trails off as he stares down at the street that's far below us."
    show i close hurt_
    ICARUS "I'm sorry, I can't."
    show i farther -close idle hurt
    "He steps back."

    ILORA "But you're a ghost! Nothing's gonna happen to you."
    show i worry_ idle
    ICARUS "I– I know…"
    """
    You don't want to confront him again, but he's being completely irrational. 

    Is he… afraid of heights? Trauma? Well, he certainly doesn't have head trauma from a fall; his ghost form looks perfectly unharmed. Yet the fact he won't come down remains.

    He's trapped in this condo room, and you're trapped with him.

    Wordlessly, you go back inside and he follows like a pup.
    """
    scene black with dissolve
    with fade   
    """
    You wonder why he's stuck here, but if superstition is worth anything maybe he's doing his penance. 

    With his gentle appearance and slightly awkward disposition, what sort of regrets could he have heavy enough to ground him to this place? 
    """
    scene door with dissolve
    with fade
    "His sole passtime seems to be making paper cranes, or that's the only thing you've seen him do one time or another. The art piece lies right where you left it, and you contemplate as you pick it up."
    
    ILORA "Making paper cranes seems to be your favorite thing to do."
    show i thinking_ at center with dissolve
    ICARUS "Well, I don't know if it's my 'favorite' but I suppose it's just something I feel I should do."

    ILORA "Why?" 
    show i -thinking_ serious idle 
    ICARUS "I don't know."
    show i light sad idle
    ICARUS "I… don't remember."

    ILORA "I see…"

    "You smile faintly at the pretty origami in your hand, recalling a certain custom."
    show i -sad
    ILORA "You know, somewhere in Japan, there's a myth about cranes."
    show i light happy idle
    ICARUS "Like this one?"

    "By the attentiveness in his expression you know he wants to hear about it."
    show i closer
    ILORA "Senbazuru. One person or a group makes one thousand paper cranes in a year and by the end of it whispers a wish. Once it's done, what they hope for will become true."

    ILORA "That's what they say anyway…"
    show i -closer farther excited
    "Whatever skepticism you were about to express, even if light, remains unsaid as you trail off. Icarus straightens his posture, eyes gleaming with resolve." 
    show i content_
    ICARUS "I'll do it."

    ILORA "Do what?"
    show i excited_ idle
    ICARUS "The thousand cranes!"

    ILORA "Now of all times? Haven't you had the chance to make more than a few?"
    show i thinking_
    ICARUS "I think…"

    "He counts with his fingers, not even reaching the tenth number."

    ICARUS "I think I made lots before, but they went missing, or at least I don't know where they all went."

    "He had a wish before?"

    ILORA "You did something similar? What was your wish then?"
    show i giggle_ thinking_ 
    "He laughs, nervous."

    ICARUS "To move on I suppose? It's the most reasonable thing I could think of… ha, ha!"
    show i giggle_ plead_ 
    "His laughter increases."
    show i giggle_ giggle
    menu:
        "“Better doing that, rather than being a sore jumpscare”":
            $ points['blue'] += 1
            $ player_color = "blue"
            show i flowery -plead_ idle -giggle plead
            """
            His laughter cracks, but you pretend not to notice.

            You remain silent for a while, still feeling his presence nearby. He isn't actively dismaying your peace, yet an uncomfortable feeling nags at you.

            You sigh.
            """

            ILORA "Fine."

        "“Just..do your own thing, and let me do mine”":
            $ points['purple'] += 1
            $ player_color = "purple"
            
            show i flowery -plead_ idle -giggle happy
            """
            You think he's about to reassure you, confirm your statement, but he eagerly nods instead.

            You aren't scared exactly, but just can't shake off the feeling that as nice as he can be, he's still a stranger.

            He doesn't intervene as you continue your task, and you let silence stretch, but as he drifts off to a dark corner of the room, an impulse hits you.
            """

        "“I can help you with that”":
            $ points['orange'] += 1
            $ player_color = "orange"
            show i love_ -plead_ -giggle idle
            "His laughter falters, mouth now slightly agape."

    show i serious idle
    "He's listening to you."

    ILORA "I'm… I'm a busy person, but…"

    ILORA "I'll help you reach the thousand paper cranes, only in my free time."
    show i startle excited idle
    ICARUS "Are you serious?"

    ILORA "Well, am I a joke?"
    show i flowery idle giggle
    ICARUS "Not at all!"

    "He quickly clarifies, maybe afraid you'll take your helping hand from him, but he seems more overjoyed than anything."
    show i farther flowery content_ 
    ICARUS "Oh thank you!"
    show i giggle
    ILORA "You're welcome."

    "Whatever makes him happy as long as you can return to your former state of peace. The quicker he's free so are you, at least in a world where your plans go thoroughly right."
    scene black with dissolve
    with fade   
    scene door with dissolve
    with fade